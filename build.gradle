plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.ktb.howard'
version = '0.0.1-SNAPSHOT'
description = 'KTB Community Server'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    /* QueryDSL - 취약점이 패치된 커뮤니티 유지보수 버전으로 변경 */
    var queryDslVersion = "5.1.1" // 패치된 최신 버전
    implementation "io.github.openfeign.querydsl:querydsl-jpa:${queryDslVersion}:jakarta"
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:${queryDslVersion}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    /* Swagger */
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    /* Spring Security */
    implementation 'org.springframework.boot:spring-boot-starter-security'
    /* Spring Session */
    implementation 'org.springframework.session:spring-session-jdbc'
}

tasks.withType(Test).configureEach {
	useJUnitPlatform()
    // testRuntimeClasspath에서 mockito-core JAR을 찾아 -javaagent로 전달
    doFirst {
        def mockitoJar = configurations.testRuntimeClasspath
                .filter { it.name.startsWith('mockito-core-') }
                .singleFile
        jvmArgs "-javaagent:${mockitoJar.absolutePath}"
    }
}

/* QueryDSL 생성 파일 경로 설정 */
def generatedDir = layout.buildDirectory.dir('generated/querydsl')

sourceSets {
    main.java.srcDirs += [ generatedDir ]
}

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(generatedDir)
}

clean.doLast {
    project.delete(generatedDir)
}